/**
 * =====================================
 * 
 * Test if the JSON contains the give property
 *
 * @param {object} json a valid JSON object
 * @param {string} name the property to test (can be in form body.path.item)
 *
 * @returns {boolean} 
 *
 * @module fm-json-additions
 * @see https://github.com/geistinteractive/fm-json-additions
 * 
 * @history 2017-10-12, todd@geistinteractive.com, fixed depth gh #14
 * @history  2017-05-16, john@e-rwu.com, added path depth, gh #3
 * @history  2017-04-09, todd@geistinteractive.com
 *
 * =====================================
 */


Case ( PatternCount ( name ; "." ) > 0 and Length ( name ) >= 3 ; 

	JSON.ContainsProperty ( JSONGetElement ( json ; GetValue ( Substitute ( name ; "." ; Â¶ ) ; 1 ) ) ; Middle ( name ; Position ( name ; "." ; 1 ; 1 ) + 1 ; Length ( name ) ) )

;

Let ( [

	keys = JSONListKeys ( json ; "" )

] ;

	not IsEmpty ( FilterValues ( name ; keys ) )

)
)