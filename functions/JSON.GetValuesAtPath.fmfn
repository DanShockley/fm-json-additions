/**
 *
 * gets a list of values from the path
 *
 * @param {array} array
 * @param {string} path or property
 *
 * @returns {string} list of the values
 * 
 * @package JSON - Extensions to the Native JSON Functions
 *
 * @author todd@geistinteractive.com
 * @version 20170516
 *
 */



Let ( [

	notValid = Left ( JSONFormatElements ( array ) ; 1 ) = "?" ;
	c = ValueCount ( JSONListValues ( array ; "" ) ) ;
	
	thisItem = JSONGetElement ( array ;  c - 1  ) ;
	newArray = JSONDeleteElement ( array ; c - 1 ) ;
	prop = JSONGetElement ( thisItem  ; path ) ;
	prop = If ( Left ( prop ; 1 ) = "?" ; ""; prop )

];

	Case (
		c < 1 or notValid ; "" ;
        		List ( JSON.GetValuesAtPath ( newArray ; path )  ; prop  )
	) //end case

) //end let